// jbmesh-tools

plugins {
    id 'java'
    id 'application'
    id 'distribution'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
    args = []
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    jvmArgs '-Xms512m', '-Xmx4096m'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

mainClassName = 'ch.alchemists.jbmesh.tools.Main'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation project(":jbmesh-lib")

    implementation 'org.jmonkeyengine:jme3-core:'    + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion

    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:'     + jmonkeyengineVersion

    // Lemur GUI library
    // https://mvnrepository.com/artifact/com.simsilica/lemur
    implementation group: 'com.simsilica', name: 'lemur', version: '1.16.0'

    // https://mvnrepository.com/artifact/com.simsilica/lemur-proto
    implementation group: 'com.simsilica', name: 'lemur-proto', version: '1.13.0'

    // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'

    // Logging dependencies
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
}

// Copy logback.xml to the build output
task copyLogbackConfig(type: Copy) {
    from "${projectDir}/src/main/resources/logback.xml"
    into "${buildDir}/resources/main"
}

// Ensure the logback.xml is copied before the JAR is created
processResources.dependsOn copyLogbackConfig